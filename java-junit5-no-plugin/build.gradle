plugins {
    id 'java'
    id 'jvm-test-suite'
}

repositories {
    mavenCentral()
}

testing {
    suites {
        configureEach {
            useJUnitJupiter()
            targets.configureEach {
                testTask.configure {
                    testLogging {
                        events = ["passed", "failed", "skipped"]
                        exceptionFormat = "full"
                    }
                }
            }
        }

        integrationTest(JvmTestSuite) {
            testType = TestSuiteType.INTEGRATION_TEST

            sources {
                compileClasspath += sourceSets.test.output + sourceSets.main.output
                runtimeClasspath += sourceSets.test.output + sourceSets.main.output
            }

            targets {
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                    }
                }
            }
        }
    }
}

tasks.named('check') {
    dependsOn(testing.suites.integrationTest)
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    testLogging {
        events = ["passed", "failed", "skipped"]
        exceptionFormat = "full"
    }
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.11.0")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.11.0")
    // just a sample dependency to prove correctness
    integrationTestImplementation("com.coditory.quark:quark-context:0.1.22")
}